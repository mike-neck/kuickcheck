import groovy.text.SimpleTemplateEngine

task funGenerator {
    def funcKt = file('src/main/generated/org/mikeneck/kuickcheck/generator/function/FunctionUtil.kt')
    def apiKt = file('src/main/generated/org/mikeneck/kuickcheck/Functions.kt')
    outputs.file(funcKt)
    outputs.file(apiKt)
    outputs.upToDateWhen {
        funcKt.exists() && apiKt.exists()
    }
    def dir = funcKt.parentFile
    def engine = {
        new SimpleTemplateEngine()
    }
    doLast {
        def range = 0..22

        def val = range.collect {
            "${it % 4 == 0 ? '\n            ' : ''}Function${it}::class.java"
        }.join(', ')

        def funInfo = range.collect {
            it == 0 ? [idx: it, param: []] :
                    [param: new IntRange(1, it).collect { "P$it" }, idx: it]
        }.collect {
            def type = it.param.join(', ')
            def v = it.param.collect { "in $it" }.join(', ')
            def zero = type.isEmpty()
            def param = "<${type}${zero ? '' : ', '}R>"
            def vp = "<${v}${zero ? '' : ', '}out R>"
            [idx: it.idx, type: type, param: param, var: vp]
        }

        def funTemplate = parent.file('template/function.txt')
        def fun = funInfo.collect {
            engine().createTemplate(funTemplate).make(it).toString()
        }.join('\n')

        def classTemplate = parent.file('template/generator.txt')
        def classes = funInfo.collect {
            engine().createTemplate(classTemplate).make(it).toString()
        }.join('\n')

        def map = [val: "    val functions = listOf($val)", fun: fun, classes: classes]

        def funKtTemplate = parent.file('template/head.txt')
        def funContents = engine().createTemplate(funKtTemplate).make(map).toString()

        if (!dir.exists()) {
            dir.mkdirs()
        }
        funcKt.write(funContents, 'UTF-8')

        def apiFunTemplate = parent.file('template/api-fun.txt')
        def apiFun = funInfo.collect {
            engine().createTemplate(apiFunTemplate).make(it)
        }.join('\n')

        def apiMap = [fun: apiFun]

        def apiKtTemplate = parent.file('template/api.txt')
        def apiContents = engine().createTemplate(apiKtTemplate).make(apiMap).toString()
        apiKt.write(apiContents, 'UTF-8')
    }
}

